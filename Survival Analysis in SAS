libname project 'C:\Users\sarad\OneDrive\Desktop\School\MSAB\BS852\Project';

proc import file = "C:\Users\sarad\OneDrive\Desktop\School\MSAB\BS852\Project\framdat4.csv"
out = project.fd
dbms = CSV REPLACE;
getnames = YES; datarow = 2;
run;

**Model Building - identify covariates and confounders**;

*Drop MENO4 because it is only relevant for females;
*Drop T2D and T2D_SURV because we do not need these variables for our analysis;
data project.fd1;
set project.fd (drop = MENO4 T2D T2D_SURV);
run;

*BMI and WGT4 likely correlated - test with proc corr;
proc corr data = project.fd1 spearman;
var BMI4 WGT4;
run;
*correlation coeff is .79 --> they are collinear so choose 1 over other

*Then see which one creates the best model fit using AIC;

*CIGS4 and SMOKE;
proc corr data = project.fd1 spearman;
var CIGS4 SMOKE;
run;
*correlation coeff is .945 --> they are collinear so choose 1 over other

*SPF4 and DPF4;
proc corr data = project.fd1 spearman;
var SPF4 DPF4;
run;
*correlation coeff is .759 --> they are collinear. Since SPF4 is predictor of interest, drop DPF4;

data project.fd2;
set project.fd1 (drop = DPF4);
run;

*Univariate survival analysis to determine which variables are associated with mortality;

proc phreg data = project.fd2;
MODEL SURV*DTH(0) = SEX / RL TIES = EFRON;
RUN;
*sex is significant, p<.0001 so keep;

proc phreg data = project.fd2;
MODEL SURV*DTH(0) = AGE4 / RL TIES = EFRON;
RUN;
*AGE IS SIGNIFICANT, P<.0001 SO KEEP;

proc phreg data = project.fd2;
MODEL SURV*DTH(0) = CHOL4 / RL TIES = EFRON;
RUN;
*CHOL4 IS SIGNIFICANT, P=.0006 SO KEEP;

proc phreg data = project.fd2;
MODEL SURV*DTH(0) = CIGS4 / RL TIES = EFRON;
RUN;
*CIGS4 significant, p = 0.0335;

proc phreg data = project.fd2;
MODEL SURV*DTH(0) = WGT4 / RL TIES = EFRON;
RUN;
*WGT4 is significantly associated with death, p<.0001 - run 1 final model with BMI4 and 1 with WGT4 and compare AIC;

proc phreg data = project.fd2;
MODEL SURV*DTH(0) = FVC4 / RL TIES = EFRON;
RUN;
*FVC4 IS SIGNIFICANT, P<.0001, SO KEEP;

proc phreg data = project.fd2;
model SURV*DTH(0) = BMI4 / RL TIES = EFRON;
RUN;
*BMI4 is significantly associated with death, p<.0001;

proc phreg data = project.fd2;
MODEL SURV*DTH(0) = HTN4 / RL TIES = EFRON;
RUN;
*HTN4 IS SIGNIFICANT, P<.0001, SO KEEP;

proc phreg data = project.fd2;
MODEL SURV*DTH(0) = SMOKE / RL TIES = EFRON;
RUN;
*SMOKE is not significant, p = .7028 - since CIGS4 and SMOKE are collinear, drop SMOKE;

DATA PROJECT.FD3;
SET PROJECT.FD2 (DROP = SMOKE);
RUN;

PROC PRINT DATA = PROJECT.FD3 (OBS = 25);
RUN;


*CHECK IF ANY VARIABLES CONFOUND THE ASSOCIATION BETWEEN SPF4 AND DEATH;
PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SPF4 / RL TIES = EFRON;
RUN;
*CRUDE HR = 1.022;

PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SPF4 SEX / RL TIES = EFRON;
RUN;
*ADJ HR = 1.023 - NO CONFOUNDING;

PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SPF4 AGE4 / RL TIES = EFRON;
RUN;
*ADJ HR = 1.013 - NO CONFOUNDING;

PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SPF4 CHOL4 / RL TIES = EFRON;
RUN;
*ADJ HR = 1.022 - NO CONFOUNDING;

PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SPF4 CIGS4 / RL TIES = EFRON;
RUN;
*ADJ HR = 1.023 - NO CONFOUNDING;

PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SPF4 WGT4 / RL TIES = EFRON;
RUN;
*ADJ HR = 1.021 - NO CONFOUNDING;

PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SPF4 FVC4 / RL TIES = EFRON;
RUN;
*ADJ HR = 1.020 - NO CONFOUNDING;

PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SPF4 BMI4 / RL TIES = EFRON;
RUN;
*ADJ HR = 1.023 - NO CONFOUNDING;

PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SPF4 HTN4 / RL TIES = EFRON;
RUN;
*ADJ HR = 1.021 - NO CONFOUNDING;

*NO VARIABLES CONFOUND THE ASSOCIATION BETWEEN SPF4 AND DEATH.
*SMOKE WAS REMOVED BECAUSE IT IS CORRELATED WITH CIGS4, AND WHILE WE FOUND VIA UNVARIATE ANALYSIS THAT CIGS4 
*IS SIGNIFICANTLY ASSOCIATED WITH DEATH, SMOKE WAS NOT. SO SMOKE WAS REMOVED.

*SINCE ALL OTHER VARIABLES ARE SIGNIFICANT VIA UNIVARIATE ANALYSIS, TEST FOR OVERALL SIGNIFICANCE
* WHEN ALL INCLUDED TOGETHER IN A COX PH MODEL AND SEE WHICH ARE SIGNIFICANT VIA WALD TEST

 *RUN FULL MODEL WITH ALL COVARIATES INCLUDED WITH BMI AND GET AIC;
PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SEX AGE4 CHOL4 CIGS4 SPF4 FVC4 BMI4 HTN4 / RL TIES = EFRON;
RUN;
*AIC = 7243.438;

*RUN FULL MODEL WITH ALL COVARIATES INCLUDED WITH WGT4 AND GET AIC;
PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SEX AGE4 CHOL4 CIGS4 SPF4 FVC4 WGT4 HTN4 / RL TIES = EFRON;
RUN;
*AIC = 7243.438 - THEY'RE THE SAME

*IN THE FULL MODEL, THE FIT OF THE MODEL IS THE SAME WHETHER BMI4 OR WGT4 WAS INCLUDED. SINCE THEY'RE COLLINEAR
*WITH ONE ANOTHER AND NOT SIGNIFICANT BY WALD TEST, DROP BOTH OF THEM.;

PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SEX AGE4 CHOL4 CIGS4 SPF4 FVC4 HTN4 / RL TIES = EFRON;
RUN;

*USING THE WALD TEST FOR SPECIFIC VARIABLES, WE FIND THAT CHOL4 AND HTN4 ARE NOT SIGNIFICANT WHILE
*ALL OF THE OTHER VARIABLES ARE. SO REMOVE CHOL4 AND HTN4;

PROC PHREG DATA = PROJECT.FD3;
MODEL SURV*DTH(0) = SEX AGE4 CIGS4 SPF4 FVC4 / RL TIES = EFRON;
RUN;

*IN THIS MODEL, ALL COVARIATES ARE SIGNIFICANT. ;
*CHECK THAT NONE OF THE FINAL COVARIATES ARE CORRELATED;

PROC CORR DATA = PROJECT.FD3 SPEARMAN;
VAR SEX AGE4 CIGS4 SPF4 FVC4;
RUN;

*WE FIND THAT NO CORRELATION COEFFICIENTS ARE GREATER THAN .6 THEREFORE THEY ARE NOT COLLINEAR
*WE FIND THAT SPF4 AND SEX HAVE A SPEARMAN CORRELATION COEFFICIENT OF .582 - SO THEY ARE HIGHLY ASSOCIATED, BUT 
*NOT COLLINEAR.;

*CHECK FINAL MODEL SELECTION WITH FORWARD AND STEPWISE ON DATA THAT HAS NOT HAD ANY VARIABLES REMOVED;
PROC PHREG DATA = PROJECT.FD1;
MODEL SURV*DTH(0) = SEX AGE4 CHOL4 CIGS4 SPF4 DPF4 WGT4 FVC4 BMI4 HTN4 SMOKE / RL TIES = EFRON
SELECTION = STEPWISE INCLUDE = 1 SLENTRY = 0.15 SLSTAY = 0.15;
RUN;
*KEEP SEX, AGE4, CIGS4, SPF4, FVC4;

PROC PHREG DATA = PROJECT.FD1;
MODEL SURV*DTH(0) = SEX AGE4 CHOL4 CIGS4 SPF4 DPF4 WGT4 FVC4 BMI4 HTN4 SMOKE / RL TIES = EFRON
SELECTION = FORWARD INCLUDE = 1 SLENTRY = 0.15;
RUN;
*KEEP SEX, AGE4, CIGS4, SPF4, FVC4;

*CONCLUSION - FINAL MODEL WILL INCLUDE SEX, AGE4, CIGS4, SPF4, FVC4;



*MAKE A NEW DATASET WITH ONLY PREDICTORS FROM FINAL MODEL;
DATA PROJECT.FD4;
SET PROJECT.FD3 (DROP = CHOL4 WGT4 BMI4 HTN4);
RUN;


proc means data=project.fd4 nmiss;
run;

*Check that I'm deleting values correctly between two methods;
data project.clean1;
set project.fd4 (drop = CHD CHD_SURV);
if cmiss(of _all_) then delete;
run;

proc sql;
select count(*) as row_count
from project.clean1;
quit;

**Delete missing data for all of the chosen covariates**;
data project.clean;
set project.fd4;
if not missing(SEX) and not missing(AGE4) and not missing(CIGS4) and not missing(SPF4) and not missing(FVC4)
and not missing(DTH) and not missing(SURV); /*keep rows where all of the covariates in the model are not missing*/
run;

proc sql;
select count(*) as row_count
from project.clean;
quit;
*looks like both ways gives 1964 rows;

*CHECK PH ASSUMPTION FOR FINAL MODEL;
PROC PHREG DATA = PROJECT.CLEAN ZPH;
MODEL SURV*DTH(0) = SPF4 SEX AGE4 CIGS4 FVC4 / RL TIES = EFRON;
RUN;
*AGE FAILS THE PH ASSUMPTION;
*SINCE AGE IS NOT THE PREDICTOR OF INTEREST, AND IT IS A CONTINUOUS VARIABLE,
*WE WILL CREATE A STRATA AND DO STRATIFIED ANALYSIS;

*visualize the age variable using a histogram;
proc sgplot data = project.clean;
histogram AGE4;
run;

*make strata for age 0-40, 40-50, 50-60, 60-70;
data project.clean;
set project.clean;
if AGE4 < 40 then agegrp = '0-40';
else if AGE4 >= 40 and AGE4 < 50 then agegrp = '40-50';
else if AGE4 >=50 and AGE4 < 60 then agegrp = '50-60';
else if AGE4 >=60 then agegrp = '60+';
run;

proc print data = project.clean (obs = 25);
run;

*RE-CHECK PH ASSUMPTION FOR FINAL MODEL;
PROC PHREG DATA = PROJECT.CLEAN ZPH;
MODEL SURV*DTH(0) = SPF4 SEX AGE4 CIGS4 FVC4 / RL TIES = EFRON;
STRATA agegrp;
RUN;

*Now all variables satisfy the PH assumption;
*SEE SLIDE 64 FOR INTERPRETATION

**Pre-Analysis: Summary of Patient Characteristics - based on included variables**;
PROC MEANS DATA = PROJECT.CLEAN N MIN MAX MEDIAN MEAN STD;
CLASS SEX;
VAR AGE4 CIGS4 SPF4 FVC4;
RUN;

/* QUESTION 1 - IS SPF4 ASSOCIATED WITH MORTALITY */
*CRUDE COX PH MODEL;
*FIRST CHECK IF PH ASSUMPTION IS MET;

PROC PHREG DATA = PROJECT.CLEAN ZPH;
MODEL SURV*DTH(0) = SPF4 / RL TIES = EFRON;
STRATA AGEGRP;
RUN;
*FAIL TO REJECT NULL, SO NO DEPARTURE FROM PH ASSUMPTION;
PROC PHREG DATA = PROJECT.CLEAN;
MODEL SURV*DTH(0) = SPF4 / RL TIES = EFRON;
STRATA agegrp;
RUN;


*WALD TEST OF SPECIFIC VARIABLE AND GLOBAL NULL HYPOTHESIS ARE CONSISTENT, SPF4 IS SIGNIFICANTLY ASSOCIATED
*WITH MORTALITY. WITH EVERY UNIT INCREASE IN SPF4, THE HAZARD OF DEATH INCREASES BY 1.014 UNITS within the same
* age stratum. Note that we cannot compare hazard of death based on SPF4 unit increases for subjects in 
* different age strata;

/* QUESTION 2 - WHAT FACTORS CAN CONFOUND THE ASSOCIATION BETWEEN SPF4 AND MORTALITY */
*bivariate analysis for confounding with the predictors included in the final model*;

proc phreg data = project.clean;
model SURV*DTH(0) = SPF4 / rl ties = efron;
STRATA agegrp;
run;
*crude HR for death comparing 1 unit increase in SPF4 is 1.014 within the same age stratum.;

proc phreg data = project.clean;
model SURV*DTH(0) = SPF4 SEX / rl ties = efron;
STRATA agegrp;
run;
*adjusted HR for death comparing 1 unit increase in SPF4 is 1.016 within the same age stratum;

proc phreg data = project.clean;
model SURV*DTH(0) = SPF4 AGE4 / rl ties = efron;
STRATA agegrp;
run;
*adjusted HR for death comparing 1 unit increase in SPF4 is 1.013 within the same age stratum;

proc phreg data = project.clean;
model SURV*DTH(0) = SPF4 CIGS4 / rl ties = efron;
STRATA agegrp;
run;
*adjusted HR for death comparing 1 unit increase in SPF4 is 1.015 within the same age stratum;

proc phreg data = project.clean;
model SURV*DTH(0) = SPF4 FVC4 / rl ties = efron;
STRATA agegrp;
run;
*adjusted HR for death comparing 1 unit increase in SPF4 is 1.015 within the same age stratum;

proc phreg data = project.clean;
model SURV*DTH(0) = SPF4 SEX AGE4 CIGS4 FVC4 / rl ties = efron;
STRATA agegrp;
run;
*adjusted HR for death comparing 1 unit increase in SPF4 is 1.014 within the same age stratum;

*CONCLUSION - no confounding by any of the covariates in the final model;

/* Question 3 - Is the association between SPF4 and mortality the same in males and females? */


proc phreg data = project.clean;
class SEX (REF = "1");
model SURV*DTH(0) = SPF4|SEX AGE4 CIGS4 FVC4 / RL TIES = EFRON;
HAZARDRATIO SPF4 / AT (SEX = ALL);
STRATA AGEGRP;
RUN;

*CONCLUSION: WE FIND THAT THE INTERACTION TERM BETWEEN SPF4 AND SEX IS NOT SIGNIFICANT AT A SIGNIFICANCE
* LEVEL OF .05 (CHISQ = 0.0435 AND P = .8347 ON 1 DF), AFTER ADJUSTING FOR AGE4, CIGS4, AND FVC4. THEREFORE,
* WE CONCLUDE THAT THE HAZARD OF DEATH FOR 1 UNIT INCREASE IN SPF4 DOES NOT CHANGE BASED ON SEX. THE HAZARD
* OF DEATH FOR 1 UNIT INCREASE IN SPF4 INCREASES BY 1.014 IN BOTH MALES AND FEMALES (95% CI = 1.009,1.019)

/* Question 4 - is CHD associated with mortality? */ 

*Create new dataset excluding rows where there are missing values for CHD, SURV, and DTH;

data project.chd;
set project.fd1;
IF NOT MISSING (CHD) AND NOT MISSING (DTH) AND NOT MISSING (SURV); /* KEEP ROWS WITHOUT MISSING VALUES FOR CHD, SURV, DTH */
RUN;

proc lifetest method = KM plot = survival data = project.chd;
time SURV*DTH(0);
STRATA CHD;
RUN;

proc phreg data = project.chd zph;
model SURV*DTH(0) = CHD / rl ties = efron;
run;
